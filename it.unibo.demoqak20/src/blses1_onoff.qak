/*
 * This model gives essential and relevant information about
 * 	     STRUCTURE, INTERACTION and BEHAVIOR
 * about the system
 * 
 * The model is EXECUTABLE
 */

System -msglog blses1_onoff
 	//"tcp://mqtt.eclipse.org:1883"
	//mqtt.eclipse.org
	//tcp://test.mosquitto.org
	//mqtt.fluux.io
	//"tcp://broker.hivemq.com" 
//mqttBroker "broker.hivemq.com" :  1883 

Event    local_buttonCmd : local_buttonCmd(X)  
Event    end : end(X)  

Dispatch ledCmd : ledCmd(X)  
  
Context ctxblses1  ip [host="localhost" port=8075] 		 
    


QActor blsonoff context ctxblses1 {
	[# 
		var odd         = true
		var toutCounter = 0
	#]
	State s0 initial { 	
		qrun resources.bls.kotlin.buttonEventEmitter.create( myself, "clickOnOff")  
	}
	Goto work
	State work{
		
	}
	Transition t0 
		whenTime 3000 -> tout
		whenEvent local_buttonCmd -> turnLed
		whenEvent end -> endOfWork
		 
	State endOfWork{
		println("blscontrol BYE")
		terminate 0
 	}
 	State turnLed{ 
 		println("blscontrol turnLed")
 		if[# odd #]{
			updateResource [# "ledCmdOn" #]
			forward led -m ledCmd : ledCmd(on)
  		}else{
			updateResource [# "ledCmdOff" #]
			forward led -m ledCmd : ledCmd(off)  			
  		}
		[# odd = !odd #]
 	}
 	Goto work


	State tout{ 
		println( "CONTROL: Are you still there ... ?  ")
		[# toutCounter++ #]
	} Goto work //if "toutCounter<4" else endOfWork
}	
 
QActor led context ctxblses1 {  
[# 
	var counter = 0 
	val leddev = resources.bls.kotlin.led.create()	
#]    
	State s0 initial {  
		//run resources.it.unibo.bls.devices.gui.LedAsGui.create( )
 		println("led started")
 		[# leddev.turnOff() #]
	} 
	Goto waitCmd   
	
	State waitCmd{  println("led waits ...") 
	}   
	Transition t0     
 		whenMsg ledCmd            -> handleLedCmd
  	  
	State  handleLedCmd{  
		printCurrentMessage   
		
		onMsg ( ledCmd : ledCmd(X) ) { 
			[# val Cmd = payloadArg(0)
			   counter++
			#]
			if[# Cmd == "on" #] {  
 				println( "led ON counter=$counter" )
 				[# leddev.turnOn() #]
 				updateResource [# "ledOn" #]
			}else{
				println( "led OFF counter=$counter" )
				[# leddev.turnOff() #]
				updateResource [# "ledOff" #]
			}
		}
	}	
	Goto waitCmd        
}
 
 